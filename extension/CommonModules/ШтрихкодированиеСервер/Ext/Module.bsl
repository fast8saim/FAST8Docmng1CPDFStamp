
&Вместо("ПолучитьДанныеДляВставкиШтрихкодаВОбъект")
Функция FAST8pdf_ПолучитьДанныеДляВставкиШтрихкодаВОбъект(Знач Объект, НеобходимоПередаватьСодержимоеФайла, ВызовСКлиента)
	
	Ссылка = Объект.Ссылка;
	ДанныеШК = Новый Структура;
	
	//Получаем и вставляем персональные настройки окна положения ШК
	НастройкиШтрихкода = ШтрихкодированиеСервер.ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице();
	ДанныеШК.Вставить("НастройкиШтрихкода", НастройкиШтрихкода);
	ДанныеШК.Вставить("ДвоичныеДанныеИзображения",	Неопределено);
	ДанныеШК.Вставить("ДвоичныеДанныеФайла",		Неопределено);
	ДанныеШК.Вставить("ПоложениеНаСтранице",		ПредопределенноеЗначение("Перечисление.ВариантыРасположенияШтрихкода.ПравыйНижний"));
	ДанныеШК.НастройкиШтрихкода.ПоказыватьФормуНастройки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Файлы.ВладелецФайла КАК ВладелецФайла,
	|	Файлы.Редактирует КАК Редактирует,
	|	Файлы.ТекущаяВерсияРасширение КАК ТекущаяВерсияРасширение
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Файлы") И ТипЗнч(Выборка.ВладелецФайла) = Тип("СправочникСсылка.ВходящиеДокументы") И ВРег(Выборка.ТекущаяВерсияРасширение) = "PDF" Тогда
		
		ДанныеШК.Вставить("ФайлРедактируется", Не Выборка.Редактирует.Пустая());
		ДанныеШК.Вставить("Расширение", Выборка.ТекущаяВерсияРасширение);
		
		Результат = ДанныеШК;
	Иначе
		Результат = ПродолжитьВызов(Объект, НеобходимоПередаватьСодержимоеФайла, ВызовСКлиента);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // FAST8pdf_ПолучитьДанныеДляВставкиШтрихкодаВОбъект()

&Вместо("ВставитьРегистрационныйШтамп")
Функция FAST8pdf_ВставитьРегистрационныйШтамп(ТекущийФайл, ДанныеОШтрихкодеФайла, ТекстНадписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Файлы.ВладелецФайла КАК ВладелецФайла,
	|	Файлы.Редактирует КАК Редактирует,
	|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия,
	|	Файлы.ТекущаяВерсия.Том КАК Том,
	|	Файлы.ТекущаяВерсия.ПутьКФайлу КАК ПутьКФайлу,
	|	Файлы.ТекущаяВерсия.ТипХраненияФайла КАК ТипХраненияФайла,
	|	Файлы.ТекущаяВерсияРасширение КАК ТекущаяВерсияРасширение,
	|	Файлы.ТекущаяВерсия.Комментарий КАК Комментарий,
	|	ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК Справочник.ВходящиеДокументы).РегистрационныйНомер КАК РегистрационныйНомер,
	|	ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК Справочник.ВходящиеДокументы).ДатаРегистрации КАК ДатаРегистрации
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ТекущийФайл);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ТипЗнч(ТекущийФайл) = Тип("СправочникСсылка.Файлы") И ТипЗнч(Выборка.ВладелецФайла) = Тип("СправочникСсылка.ВходящиеДокументы") И ВРег(Выборка.ТекущаяВерсияРасширение) = "PDF" Тогда
		
		Если Выборка.Комментарий <> "Вставка регистрационного номера" Тогда
			ИмяФайлаСПутем = ПолучитьИмяВременногоФайла(Выборка.ТекущаяВерсияРасширение);		
			
			Если Выборка.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
				
				ХранилищеФайла = РаботаСФайламиВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(Выборка.ТекущаяВерсия);
				ДвоичныеДанныеФайла = ХранилищеФайла.Получить();
				ДвоичныеДанныеФайла.Записать(ИмяФайлаСПутем);
				
			Иначе 
				
				Если Не Выборка.Том.Пустая() Тогда
					ИмяФайлаСПутемВТоме = ФайловыеФункции.ПолныйПутьТома(Выборка.Том) + Выборка.ПутьКФайлу; 
					КопироватьФайл(ИмяФайлаСПутемВТоме, ИмяФайлаСПутем);
					Файл = Новый Файл(ИмяФайлаСПутем);
					Файл.УстановитьТолькоЧтение(Ложь);
				КонецЕсли;
				
			КонецЕсли;
			
			ИмяФайлаРезультата = ПолучитьИмяВременногоФайла(Выборка.ТекущаяВерсияРасширение);
			ИмяФайлаЛога = ПолучитьИмяВременногоФайла("txt");
						
			Команда = СтрШаблон("python fast8_docmng_base.py 1cdocmng ""%1"" ""%2"" 380 50 ""Рег. № %3"" ""От %4"" %5",
				ИмяФайлаСПутем,
				ИмяФайлаРезультата,
				Выборка.РегистрационныйНомер,
				Формат(Выборка.ДатаРегистрации, "ДЛФ=Д"),
				ИмяФайлаЛога
				);
									
			КодВозврата = Неопределено;
			ЗапуститьПриложение(Команда, "F:\python_docmng\", Истина, КодВозврата);
			
			Результат		= Ложь;
			ФайлОбработан	= Ложь;
			ПроверкаФайла = Новый Файл(ИмяФайлаРезультата);
			Если ПроверкаФайла.Существует() Тогда
				ДвоичныеДанныеРезультата = Новый ДвоичныеДанные(ИмяФайлаРезультата);
				ФайлОбработан = Истина;
			КонецЕсли;
			
			ПроверкаЛога = Новый Файл(ИмяФайлаЛога);
			Если ПроверкаЛога.Существует() Тогда
				ЛогВыполнения = Новый ТекстовыйДокумент;
				ЛогВыполнения.Прочитать(ИмяФайлаЛога);
				ТекстОшибки = ЛогВыполнения.ПолучитьТекст();
				Если ЗначениеЗаполнено(ТекстОшибки) Тогда
					ЗаписьЖурналаРегистрации("FAST8 Штамп PDF", УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.ВходящиеДокументы, Выборка.ВладелецФайла, ТекстОшибки);
					ВызватьИсключение СтрШаблон("Ошибка установки штампа в PDF:%1%2", Символы.ПС, ТекстОшибки);
				КонецЕсли;
			КонецЕсли;
						
			Если ФайлОбработан Тогда
				АвтозаполнениеШаблоновФайловСервер.ОбновитьВерсиюИзДвоичныхДанных(ДвоичныеДанныеРезультата, ТекущийФайл, "Вставка регистрационного номера");
				Результат = Истина;
			КонецЕсли;
			
			УдаляемыеФайлы = Новый Массив;
			УдаляемыеФайлы.Добавить(ИмяФайлаСПутем);
			УдаляемыеФайлы.Добавить(ИмяФайлаРезультата);
			УдаляемыеФайлы.Добавить(ИмяФайлаЛога);
			Для Каждого УдаляемыйФайл Из УдаляемыеФайлы Цикл
				Попытка
					УдалитьФайлы(УдаляемыйФайл);
				Исключение
				КонецПопытки;
			КонецЦикла;
			Файл = Неопределено;
		КонецЕсли;
		
	Иначе
		Если ДанныеОШтрихкодеФайла <> Неопределено 
			И НЕ ДанныеОШтрихкодеФайла.Свойство("ДвоичныеДанныеФайла") Тогда
			ДанныеОШтрихкодеФайла = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(ТекущийФайл);
		КонецЕсли;
		
		НастройкиПоложения = ШтрихкодированиеСервер.ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице();
		Если Не ЗначениеЗаполнено(НастройкиПоложения.ПоложениеНаСтранице) Тогда
			НастройкиПоложения.ПоложениеНаСтранице = ПредопределенноеЗначение("Перечисление.ВариантыРасположенияШтрихкода.ПравыйНижний");
		КонецЕсли;
				
		Результат = ШтрихкодированиеКлиентСервер.ВставитьРегистрационныйШтампСИспользованиемНастроек(ТекущийФайл, 
			НастройкиПоложения, 
			Ложь, 
			ТекстНадписи, 
			ДанныеОШтрихкодеФайла.ДвоичныеДанныеФайла,
			ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения,
			ДанныеОШтрихкодеФайла.Расширение,
			ДанныеОШтрихкодеФайла.ФайлРедактируется, 
			ДанныеОШтрихкодеФайла.ИзменениеФайловMSWordТолькоНаСервере);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции // FAST8pdf_ВставитьРегистрационныйШтамп()
